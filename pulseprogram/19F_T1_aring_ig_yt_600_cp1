/* 19F_T1_aring_yt_500_cp1

  Written by YT Apr 25, 2022 
  Modified from 1HT1_water_lek_500_cp
  
  Used to record 19F T1 as a series of 1Ds

  This pp allows one to perform the saturation recovery experiment.
  Use 19F_CEST_ig_yt_600_cp for phase-cycled T1 measurement.

  Note 1H is always decoupled during the acquisition, while the 1H decoupling
  during the relxation period (recovery period) is optional (use -DH_dec).

  One can control the initial 1H magnetizaion (-DH_dephase or -DH_compdec).
  When these flgs are used, SL purge of 19F magnetization is not used.

  Magnetization originates as zero and measure build up (1- exp(-T/T1) profile
  
  Utilizing the anti-ringing triple pulse for excitation to relieve base-line distortion
  caused by acoustic ringing.
  
  Ref: Wang et al., JMR 2021 332 107067,  JBNMR 2020 74:753

  Modified for cryo probe

  Modified by LEK on Nov 14, 2022 to turn on lock during T1 recovery during
   sequence

  Modified by YT on Sep 11, 2023 to add the option to perform decoupling during the relaxation delay.

  Modified by YT on Dec 1st, 2023 to add the option to 1H dephase at the begining of the measurement
  to see the effect of the initial 1H magnetization.

  Modified by YT on Jan 19th, 2024 to add the option to 1H decoupling at the begining of the measurement
  to see the effect of the initial 1H magnetization.

*/

#include <Avance.incl>
#include <Grad.incl>
#include <Delay.incl>

;Define phases
#define zero ph=0.0
#define one ph=90.0
#define two ph=180.0
#define three ph=270.0
 
;Define Pulses
define pulse pwf
       "pwf=p1"             ; 1H hard pulse at power pl1

;Define delays

"in0=inf1/2"
"d11=30m"

define delay dly_lk
  "dly_lk = d17"

define delay dly_compdec
  "dly_compdec = d18"

define list<delay> time_relax = <$VDLIST>

"cnst12 = plw12"  ; power level for 1H scrambling pulse

"l2 = 0"  ; pointer to vd list for magnetization T1 recovery
"acqt0=-pwf*2.0/PI"

1 ze

; check validity of parameters

  if "cnst12 > 4.0"
  {
   2u
   print "error: power level for 1H locking is too large < 4W"
   goto HaltAcqu
  }
 
  if "dly_lk > 41m" {
   2u
   print "error: ly_lk too lone < 40 ms"
   goto HaltAcqu
  }

2 d11 do:f2 

; continue to check run time variables

"DELTA = time_relax[l2]"

 if "DELTA > 20s" {
  2u
  print "error: time_relax is too long < 20s"
  goto  HaltAcqu
  }

 2u pl1:f1                        ; power(tpwr)
 d1                               ; delay(d1)

 20u UNBLKGRAD             ; dly 20u, unblank gradients and lock hold

  
#ifdef H_dephase
 (2u pl2):f2            ; power pl2 for 1H hard pulse
 (p21 zero):f2             ; Turn OFF 1H decoupling 
 2u
 p55:gp5       ; gradient 5
 d16
 (2u pl12):f2            ; power pl12 for 1H decoupling during acquisition
#endif

#ifdef H_compdec
 (2u pl14):f2           ; power(1H deocupling)
 (2u cpds3 zero):f2     ; Turn ON 1H decoupling at power pl14
 "DELTA = dly_compdec-8u"
 DELTA
 (2u do):f2             ; Turn OFF 1H decoupling   
 (2u pl12):f2            ; power pl12 for 1H decoupling during acquisition

#endif


; dephase initial 19F magnetization

#ifndef H_compdec
#ifndef H_dephase

 2u pl11:f1
 (2u cw zero):f1
 dly_lk                   ; turn on cw decoupling for a delay of dly_lk
 2u do:f1

 2u pl11:f1
 (2u cw one):f1
 "DELTA = dly_lk/2.0"
 DELTA                   ; turn on cw decoupling for a delay of dly_lk
 2u do:f1

 2u
 p55:gp5*0.5       ; gradient 5 * 0.5
 d16
#endif
#endif

; This is the real start

 2u pl1:f1
 (pwf zero):f1

 2u
 p55:gp5       ; gradient 5
 d16

 (pwf one):f1

 2u
 p55:gp5*0.3       ; gradient 5
 d16

 4u BLKGRAD
 
#ifdef H_dec
 (2u pl13):f2           ; power(1H deocuplign during CEST)
 (2u cpds1 zero):f2     ; Turn ON 1H decoupling at power pl11
 "DELTA = time_relax[l2]"
 DELTA
 (2u do):f2             ; Turn OFF 1H decoupling   
 (2u pl12):f2            ; power pl12 for 1H decoupling during acquisition

#else
 "DELTA = time_relax[l2]"
  DELTA pl12:f2
#endif
 
 ;anti-ringing triple pulse 
 (pwf ph1):f1
 (pwf ph2):f1
 (pwf ph3):f1


  go=2 ph31 cpds2:f2        ; acquire fid 
  d11 do:f2 mc #0 to 2 F0(zd)   ; write FID to disk
  F1QF(calclc(l2,1))

HaltAcqu, 1m
exit

ph1=0 2 0 2 0 2 0 2 1 3 1 3 1 3 1 3 
ph2=2 2 0 0 2 2 0 0 3 3 1 1 3 3 1 1 
ph3=0 0 0 0 2 2 2 2 1 1 1 1 3 3 3 3
ph31=0 2 2 0 2 0 0 2 1 3 3 1 3 1 1 3

ph26=0
ph27=1
ph28=2
ph29=3

;d1 : repetition delay
;d11 : delay for disk i/o, 30ms
;d16 : gradient recovery delay, 200us
;d17 : delay for scrambling - set to 40 ms
;d18 : delay for initial dephasing (-DH_compdec).
;cpdprg1: decoupling according to sequence defined by cpdprg1 (decoupling during Trelax)
;cpdprg2: decoupling according to sequence defined by cpdprg2 (decoupling during acquisition)
;cpdprg3: decoupling according to sequence defined by cpdprg3 (for initial decoupling)
;pcpd2: f2 channel - 90 degree pulse for decoupling sequence
;pl1 : power level for 19F 90 pulse
;pl2 : power level for 1H 90 pulse 
;pl11 : power level for scrambling (19F), typically about 23dB less power than high power
;pl12 : power level for 1H decoupling during acquisition
;pl13 : power level for 1H decoupling during the relaxation period.
;pl14 : power level for 1H decoupling for initial dephasing (-DH_compdec).
;cnst12 : power in 
;p1 : 19F pulse, 90 deg
;p21 : 1H pulse, 90 deg

;zgoptns : -DH_dec -DH_dephase, -DH_compdec
