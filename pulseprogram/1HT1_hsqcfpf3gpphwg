/* 1HT1_hsqcfpf3gpphwg

  Written by YT Apr 1, 2025
  Modified from hsqcf3fpgpphwg
  
  set vdlist
  
  1H saturation recovery experiment with 2D HSQC detection.
  Iwahara and Clore, J. AM. CHEM. SOC. 2010, 132, 13346 - 13356

*/



;hsqcfpf3gpphwg
;avance-version (12/01/11)
;HSQC
;2D H-1/X correlation via double inept transfer
;phase sensitive
;with decoupling during acquisition
;using f3 - channel
;using flip-back pulse
;water suppression using watergate sequence
;similar to fhsqc 
;(use parameterset HSQCFPF3GPPHWG)
;
;G. Bodenhausen & D.J. Ruben, Chem. Phys. Lett. 69, 185 (1980)
;M. Piotto, V. Saudek & V. Sklenar, J. Biomol. NMR 2, 661 - 666 (1992)
;V. Sklenar, M. Piotto, R. Leppik & V. Saudek, J. Magn. Reson.,
;   Series A 102, 241 -245 (1993)
;S. Mori, C. Abeygunawardana, M. O'Neil-Johnson & P.C.M. van Zijl,
;   J. Magn. Reson. B 108, 94-98 (1995)
;
;$CLASS=HighRes
;$DIM=2D
;$TYPE=
;$SUBTYPE=
;$COMMENT=


prosol relations=<triple>

  
#include <Avance.incl>
#include <Delay.incl>
#include <Grad.incl>

;aqseq 321

;Define phases
#define zero ph=0.0
#define one ph=90.0
#define two ph=180.0
#define three ph=270.0

"p2=p1*2"
"p22=p21*2"
"d11=30m"
"d12=20u"
"d26=1s/(cnst4*4)"


"d0=3u"

"in0=inf1/2"


"DELTA1=d26-p16-d16"
"DELTA2=d26-p16-d16-p14-12u"
"DELTA=d0*2+p2"

define list<delay> time_relax = <$VDLIST>
"l2 = 0"  ; pointer to vd list for magnetization T1 recovery

1 ze 
  d11 pl16:f3
2 d1 do:f3
  d12 pl1:f1 pl3:f3
  50u UNBLKGRAD
 
#ifdef F_dephase
  (p21 zero):f3
  2u
  p55:gp1
  d16
#endif


#ifdef eburp
  2u fq=cnst20 (bf ppm):f1
  4u pl15:f1
  (p15:sp15 zero):f1  
  2u
  p55:gp1
  d16
  (p15:sp15 one):f1  
  2u
  p55:gp1*0.3
  d16
  4u fq=0:f1 pl1:f1  
#else
  4u pl0:f1
  (p14:sp14 two):f1  
  2u pl1:f1  
  (p1 zero):f1
  2u
  p55:gp1      
  d16
  4u pl0:f1
  (p14:sp14 three):f1  
  2u pl1:f1  
  (p1 one):f1
  2u
  p55:gp1*0.3    
  d16
  4u BLKGRAD
#endif

  "DELTA3 = time_relax[l2]-d16-4u-4u"
  DELTA3
  4u UNBLKGRAD

  (p1 ph1)
  p16:gp3
  d16
  DELTA1
  (center (p2 ph2) (p22 ph6):f3 )
  DELTA1
  p16:gp3
  d16
  (p1 ph2) 

  4u pl0:f1
  (p14:sp14 ph8:r):f1
  4u
  p16:gp4
  d16 pl1:f1

  (p21 ph3):f3
  d0
  (p2 ph5)
  d0
  (p22 ph4):f3
  DELTA
  (p21 ph4):f3

  4u
  p16:gp4
  d16 pl0:f1
  (p14:sp14 ph7:r):f1
  4u
  4u pl1:f1

  (p1 ph1) 
  4u
  p16:gp5
  d16
  DELTA2 pl0:f1
  (p14:sp14 ph7:r):f1
  4u
  4u pl1:f1
  (center (p2 ph1) (p22 ph1):f3 )
  4u pl0:f1
  (p13:sp13 ph7:r):f1
  4u
  p16:gp5
  d16
  DELTA2 pl16:f3
  4u BLKGRAD

  go=2 ph31 cpd3:f3
  d1 do:f3 mc #0 to 2 
  F2QF(calclc(l2,1))
  F1PH(calph(ph3, +90) & calph(ph6, +90), caldel(d0, +in0))
exit 
  

ph1=0
ph2=1
ph3=0 2
ph4=0 0 0 0 2 2 2 2
ph5=0 0 2 2
ph6=0
ph7=2
ph8=0
ph31=0 2 0 2 2 0 2 0


;pl0 : 0W
;pl1 : f1 channel - power level for pulse (default)
;pl3 : f3 channel - power level for pulse (default)
;pl16: f3 channel - power level for CPD/BB decoupling
;sp1: f1 channel - shaped pulse  90 degree
;sp13: f1 channel - power level for water flip up pulse
;sp14: f1 channel - power level for water flip down pulse
;sp15: f1 channel - power level for eburp pulse
;p2 : f1 channel - 180 degree high power pulse
;p8 : f2 channel - 180 degree shaped pulse for inversion (adiabatic)
;p13: f1 channel -  90 degree water flip up shaped pulse
;p14: f1 channel -  90 degree water flip down shaped pulse
;p15: f1 channel -  90 degree E-burp pulse
;p16: homospoil/gradient pulse
;p21: f3 channel -  90 degree high power pulse
;p22: f3 channel - 180 degree high power pulse
;d0 : incremented delay (2D)                         [3 usec]
;d1 : relaxation delay; 1-5 * T1
;d11: delay for disk I/O                             [30 msec]
;d12: delay for power switching                      [20 usec]
;d16: delay for homospoil/gradient recovery
;d26 : 1/(4J)YH
;cnst4: = J(YH)
;inf1: 1/SW(X) = 2 * DW(X)
;in0: 1/(2 * SW(X)) = DW(X)
;nd0: 2
;ns: 4 * n
;ds: 16
;td1: number of experiments
;FnMODE: States-TPPI, TPPI, States or QSEQ
;cpd3: decoupling according to sequence defined by cpdprg3
;pcpd3: f3 channel - 90 degree pulse for decoupling sequence
;cnst20: 1H E-burp center [ppm]

;use gradient ratio:    gp 3 : gp 4 : gp 5
;                         50 :   80 :   30

;for z-only gradients:
;gpz3: 50%
;gpz4: 80%
;gpz5: 30%

;use gradient files:   
;gpnam3: SMSQ10.100
;gpnam4: SMSQ10.100
;gpnam5: SMSQ10.100

;zgoptns : -DF_dephase, -Deburp

;$Id: hsqcfpf3gpphwg,v 1.12 2012/01/31 17:49:26 ber Exp $
